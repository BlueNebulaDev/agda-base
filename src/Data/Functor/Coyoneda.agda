{-# OPTIONS --type-in-type #-}

module Data.Functor.Coyoneda where

open import Prelude

private
  variable
    a b : Set
    f g : Set -> Set

-- This is the existensial version Yoneda f a.
data Coyoneda (f : Set -> Set) (a : Set) : Set where
  Coyoneda: : (b -> a) -> f b -> Coyoneda f a

-- The coYoneda lemma states that f a ~= Coyoneda f a. The isomorphsim
-- is witnessed by lower and lift.
lowerCoyoneda : {{_ : Functor f}} -> Coyoneda f a -> f a
lowerCoyoneda (Coyoneda: f x) = map f x

liftCoyoneda : f a -> Coyoneda f a
liftCoyoneda y = Coyoneda: id y

-- It turns out that Coyoneda is a free construction, i.e. Coyoneda f is
-- the free functor generated by f. This is the right adjunct of the
-- corresponding free/forgetful adjunction.
interpretCoyoneda : {{_ : Functor g}}
  -> (forall {a} -> f a -> g a) -> Coyoneda f b -> g b
interpretCoyoneda t (Coyoneda: f x) = map f (t x)

-- This is the left adjunct.
uninterpretCoyoneda : (forall {a} -> Coyoneda f a -> g a) -> f b -> g b
uninterpretCoyoneda t x = t (liftCoyoneda x)

instance
  Functor-Coyoneda : Functor (Coyoneda f)
  Functor-Coyoneda .map f (Coyoneda: g x) = Coyoneda: (f <<< g) x

  Applicative-Coyoneda : {{_ : Applicative f}} -> Applicative (Coyoneda f)
  Applicative-Coyoneda .pure = liftCoyoneda <<< pure
  Applicative-Coyoneda ._<*>_ (Coyoneda: f x) (Coyoneda: g y) =
    liftCoyoneda $ (| f x |) <*> (| g y |)

  Alternative-Coyoneda : {{_ : Alternative f}} -> Alternative (Coyoneda f)
  Alternative-Coyoneda .empty = liftCoyoneda empty
  Alternative-Coyoneda ._<|>_ l r =
    liftCoyoneda $ lowerCoyoneda l <|> lowerCoyoneda r

  Monad-Coyoneda : {{_ : Monad f}} -> Monad (Coyoneda f)
  Monad-Coyoneda ._>>=_ m f =
    liftCoyoneda $ lowerCoyoneda m >>= (f >>> lowerCoyoneda)
